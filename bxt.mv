<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "compresswhitespace">
	<MvASSIGN NAME = "l.module:code"		VALUE = "brandextype">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Super Fantastic Ultimate Power Magic Brand and Types">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Tess Guefen">
	<MvASSIGN NAME = "l.module:version"		VALUE = "1.000">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "9.00">
	<MvASSIGN NAME = "l.module:description"	VALUE = "omgz">
	<MvASSIGN NAME = "l.module:features"	VALUE = "data_store, util, vis_util, json, clientside, fields_prod">
</MvFUNCTION>
<mvcomment>, fields_cat, not_cat, not_prod, vis_prod, vis_cat</mvcomment>

<MvCOMMENT>
| ========================================================================================================================
|	INSTALL
| ========================================================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Install_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	===========================
	Create sXX_bxtBrands
	===========================
	</MvCOMMENT>
	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $  g.Store_Table_Prefix $ 'bxtBrands
								(
									id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
									code		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
									name		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
									image		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
									icon		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ '
								) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'BXT-INSTALL-0001', g.MvQUERY_Error ) }">
	</MvIF>
	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'bxtBrands ON ' $ g.Store_Table_Prefix $ 'bxtBrands ( id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'BXT-INSTALL-0002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	===========================
	Create sXX_bxtTypes
	===========================
	</MvCOMMENT>
	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $  g.Store_Table_Prefix $ 'bxtTypes
								(
									id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
									code		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
									name		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ '
								) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'BXT-INSTALL-0003', g.MvQUERY_Error ) }">
	</MvIF>
	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'bxtTypes ON ' $ g.Store_Table_Prefix $ 'bxtTypes ( id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'BXT-INSTALL-0004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	===========================
	Create sXX_bxtBrandxCat
	===========================
	</MvCOMMENT>
	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $  g.Store_Table_Prefix $ 'bxtBrandxCat
								(
									brand_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
									cat_id		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ '
								) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'BXT-INSTALL-0005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	===========================
	Create sXX_bxtBrandxProd
	===========================
	</MvCOMMENT>
	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $  g.Store_Table_Prefix $ 'bxtBrandxProd
								(
									brand_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
									product_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ '
								) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'BXT-INSTALL-0006', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	===========================
	Create sXX_bxtTypexCat
	===========================
	</MvCOMMENT>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $  g.Store_Table_Prefix $ 'bxtTypexCat
								(
									type_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
									cat_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ '
								) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'BXT-INSTALL-0007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	===========================
	Create sXX_bxtTypexProd
	===========================
	</MvCOMMENT>
	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $  g.Store_Table_Prefix $ 'bxtTypexProd
								(
									type_id		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
									product_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ '
								) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Library_Filename_Utilities ].Error( 'BXT-INSTALL-0007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'bxtBrands', 1 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'BXT-INSTALL-0008:', 'An error occured while creating the storekey, bxtBrands. Please make sure this does not already exsist.' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'bxtTypes', 1 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'BXT-INSTALL-0009:', 'An error occured while creating the storekey, bxtTypes. Please make sure this does not already exsist.' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Upgrade_Store" PARAMETERS = "module var, version" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Uninstall_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'bxtBrands' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'bxtTypes' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'bxtBrandxCat' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'bxtBrandxProd' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'bxtTypexCat' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'bxtTypexProd' }">

	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'bxtBrands' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'bxtTypes' ) }">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvCOMMENT>
| ========================================================================================================================
|	UTIL
| ========================================================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "StoreUtilityModule_Action" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_LeftNavigation" PARAMETERS = "module var, ident" STANDARDOUTPUTLEVEL = "compresswhitespace">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].LeftNavigation_Dot( l.indent - 1, 'Screen=SUTL&Store_Code=' $ encodeattribute( g.Store:code ) $ '&Module_Code=' $ encodeattribute( l.module:code ), 'BXT', 'Super Fantastic Ultimate Power Magic Brand and Types' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_Screen" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "html, text, compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN> </MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_Start( 'Super Fantastic Ultimate Power Magic Brand and Types', '', '' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_AdminUI_CSS() }">

	<MvIF EXPR = "{ g.BXTScreen EQ 'VIEWBRAND' AND g.View_Brand }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_CSS() }">
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Store_Code=' $ encodeattribute( g.Store_Code ) $ '&View_Brand=' $ encodeattribute( g.View_Brand ) $ '&Filename=ViewBrands_functions.js' }"></script>
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Store_Code=' $ encodeattribute( g.Store_Code ) $ '&View_Brand=' $ encodeattribute( g.View_Brand ) $ '&Filename=ViewBrands.js' }"></script>
		<script language="JavaScript">
			MMScreen_LoadFinished( function() { new ViewBrands_Batchlist(); } );
		</script>
	<MvELSEIF EXPR = "{ g.BXTScreen EQ 'VIEWTYPE' AND g.View_Type }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_CSS() }">
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Store_Code=' $ encodeattribute( g.Store_Code ) $ '&View_Type=' $ encodeattribute( g.View_Type) $ '&Filename=ViewTypes_functions.js' }"></script>
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Store_Code=' $ encodeattribute( g.Store_Code ) $ '&View_Type=' $ encodeattribute( g.View_Type) $ '&Filename=ViewTypes.js' }"></script>
		<script language="JavaScript">
			MMScreen_LoadFinished( function() { new ViewTypes_Batchlist(); } );
		</script>
	<MvELSEIF EXPR = "{ g.Tab EQ 'BXTBrands' OR ISNULL g.Tab }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_CSS() }">
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Store_Code=' $ encodeattribute( g.Store_Code ) $ '&Filename=Brands_functions.js' }"></script>
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Store_Code=' $ encodeattribute( g.Store_Code ) $ '&Filename=Brands.js' }"></script>
		<script language="JavaScript">
			MMScreen_LoadFinished( function() { new Brands_Batchlist(); } );
		</script>
	<MvELSEIF EXPR = "{ g.Tab EQ 'BXTTypes' }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_CSS() }">
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Store_Code=' $ encodeattribute( g.Store_Code ) $ '&Filename=Types_functions.js' }"></script>
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Store_Code=' $ encodeattribute( g.Store_Code ) $ '&Filename=Types.js' }"></script>
		<script language="JavaScript">
			MMScreen_LoadFinished( function() { new Types_Batchlist(); } );
		</script>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_End( 'Super Fantastic Ultimate Power Magic Brand and Types', '', '' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginContent() }">
	
	<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawTabs( 'BXT', 'BXTBrands:Brands,BXTTypes:Types' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_HTML() }">
	<MvIF EXPR = "{ g.BXTScreen EQ 'VIEWBRAND' AND g.View_Brand }">
		<div id="BXTView_Brands"></div>
	<MvELSEIF EXPR = "{ g.BXTScreen EQ 'VIEWTYPE' AND g.View_Type }">
		<div id="BXTView_Types"></div>
	<MvELSEIF EXPR = "{ g.Tab EQ 'BXTBrands' }">
		<div id="BXTBrands"></div>
	<MvELSEIF EXPR = "{ g.Tab EQ 'BXTTypes' }">
		<div id="BXTTypes"></div>
	</MvIF>

	<MvHIDE FIELDS = "g.Module_Code">

	<MvEVAL EXPR = "{ [ g.Module_Admin ].EndContent() }">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawButtons( '' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].EndScreen() }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "compresswhitespace">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>


<MvCOMMENT>
| ========================================================================================================================
|	VIS_UTIL
| ========================================================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Utility_Content" PARAMETERS = "module var, tab, load_fields" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Utility_Head" PARAMETERS = "module var, tab" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Utility_Tabs" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "compresswhitespace">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Utility_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "compresswhitespace">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Utility_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "compresswhitespace">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvCOMMENT>
| ========================================================================================================================
|	CLIENTSIDE
| ========================================================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Clientside" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ g.Filename EQ 'Brands.js' }">
		<MvINCLUDE FILE = "js/Brands.js">
	</MvIF>

	<MvIF EXPR = "{ g.Filename EQ 'Brands_functions.js' }">
		<MvINCLUDE FILE = "js/Brands_functions.js">
	</MvIF>

	<MvIF EXPR = "{ g.Filename EQ 'Types.js' }">
		<MvINCLUDE FILE = "js/Types.js">
	</MvIF>

	<MvIF EXPR = "{ g.Filename EQ 'Types_functions.js' }">
		<MvINCLUDE FILE = "js/Types_functions.js">
	</MvIF>

	<MvIF EXPR = "{ g.Filename EQ 'ViewBrands.js' }">
		<MvINCLUDE FILE = "js/ViewBrands.js">
	</MvIF>

	<MvIF EXPR = "{ g.Filename EQ 'ViewBrands_functions.js' }">
		<MvINCLUDE FILE = "js/ViewBrands_functions.js">
	</MvIF>

	<MvIF EXPR = "{ g.Filename EQ 'ViewTypes.js' }">
		<MvINCLUDE FILE = "js/ViewTypes.js">
	</MvIF>

	<MvIF EXPR = "{ g.Filename EQ 'ViewTypes_functions.js' }">
		<MvINCLUDE FILE = "js/ViewTypes_functions.js">
	</MvIF>
</MvFUNCTION>


<MvCOMMENT>
| ========================================================================================================================
|	JSON
| ========================================================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "Module_JSON" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, compresswhitespace">
	<MvIF EXPR = "{ g.Module_Function EQ 'Brands_Load_Query' }">
		<MvEVAL EXPR = "{ JSON_Brands_Load_Query( l.module ) }">
	</MvIF>

	<MvIF EXPR = "{ g.Module_Function EQ 'Brand_Insert' }">
		<MvFUNCTIONRETURN VALUE = "{ JSON_Brand_Insert( g.code, g.name, g.image, g.icon ) }">
	</MvIF>

	<MvIF EXPR = "{ g.Module_Function EQ 'Brand_Update' }">
		<MvFUNCTIONRETURN VALUE = "{ JSON_Brand_Update( g.id, g.code, g.name, g.image, g.icon ) }" />
	</MvIF>

	<MvIF EXPR = "{ g.Module_Function EQ 'Brand_Delete' }">
		<MvFUNCTIONRETURN VALUE = "{ JSON_Brand_Delete( g.Brand ) }">
	</MvIF>

	<MvIF EXPR = "{ g.Module_Function EQ 'Types_Load_Query' }">
		<MvEVAL EXPR = "{ JSON_Types_Load_Query( l.module ) }">
	</MvIF>

	<MvIF EXPR = "{ g.Module_Function EQ 'Type_Insert' }">
		<MvFUNCTIONRETURN VALUE = "{ JSON_Type_Insert( g.code, g.name ) }">
	</MvIF>

	<MvIF EXPR = "{ g.Module_Function EQ 'Type_Update' }">
		<MvFUNCTIONRETURN VALUE = "{ JSON_Type_Update( g.id, g.code, g.name ) }" />
	</MvIF>

	<MvIF EXPR = "{ g.Module_Function EQ 'Type_Delete' }">
		<MvFUNCTIONRETURN VALUE = "{ JSON_Type_Delete( g.Type ) }">
	</MvIF>

	<MvIF EXPR = "{ g.Module_Function EQ 'ViewBrands_Load_Query' }">
		<MvFUNCTIONRETURN VALUE = "{ JSON_ViewBrands_Load_Query( l.module) }" />
	</MvIF>

	<MvIF EXPR = "{ g.Module_Function EQ 'BrandXCategoryAssign' }">
		<MvFUNCTIONRETURN VALUE = "{ JSON_BrandxCat_Assign_Unassign( g.Brand_ID, g.Category_ID, g.Assign) }" />
	</MvIF>

	<MvIF EXPR = "{ g.Module_Function EQ 'ViewTypes_Load_Query' }">
		<MvFUNCTIONRETURN VALUE = "{ JSON_ViewTypes_Load_Query( l.module) }" />
	</MvIF>

	<MvIF EXPR = "{ g.Module_Function EQ 'TypeXCategoryAssign' }">
		<MvFUNCTIONRETURN VALUE = "{ JSON_TypexCat_Assign_Unassign( g.Type_ID, g.Category_ID, g.Assign) }" />
	</MvIF>
</MvFUNCTION>

<MvCOMMENT>
| ========================================================================================================================
|	FIELDS_PROD
| ========================================================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Product_Fields" PARAMETERS = "module var, fields var" STANDARDOUTPUTLEVEL = "">

	<MvASSIGN NAME = "l.fields" INDEX = "1" MEMBER = "code" VALUE = "type">
	<MvASSIGN NAME = "l.fields" INDEX = "1" MEMBER = "name" VALUE = "Type">
	<MvASSIGN NAME = "l.fields" INDEX = "2" MEMBER = "code" VALUE = "brand">
	<MvASSIGN NAME = "l.fields" INDEX = "2" MEMBER = "name" VALUE = "Brand">

	<MvFUNCTIONRETURN VALUE = 2>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Field_Name" PARAMETERS = "module var, code" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.code EQ 'type' }">
		<MvFUNCTIONRETURN VALUE = "Type">
	</MvIF>
	<MvIF EXPR = "{ l.code EQ 'brand' }">
		<MvFUNCTIONRETURN VALUE = "Brand">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Field_Value" PARAMETERS = "module var, product_id, code" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ Return_Product_Brand_Or_Type( l.product_id, l.code ) }" />
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Set_Field" PARAMETERS = "module var, product_id, code, value" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Update_Product_Brand_Or_Type( l.product_id, l.code, l.value ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>
</MvFUNCTION>


<MvCOMMENT>
| ========================================================================================================================
|	functions/brands.mv
|	functions/types.mv
|	
|	Shared functions
|	- Update_Product_Brand_Or_Type( product_id, brand_or_type, value )
|	- Return_Product_Brand_Or_Type( product_id, brand_or_type )
| ========================================================================================================================
</MvCOMMENT>

<MvINCLUDE FILE = "functions/brands.mv">
<MvINCLUDE FILE = "functions/types.mv">

<MvFUNCTION NAME = "Update_Product_Brand_Or_Type" PARAMETERS = "product_id, brand_or_type, value" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT l.product_id OR NOT l.brand_or_type OR NOT l.value }">
		<MvFUNCTIONRETURN VALUE = "" />
	</MvIF>
	<MvIF EXPR = "{ l.brand_or_type EQ 'brand' }">
		<MvEVAL EXPR = "{ Load_Brand_Code( l.value, l.brand ) }" />
		<MvIF EXPR = "{ l.brand:id }">
			<MvFUNCTIONRETURN VALUE = "{ UpdateProductBrand( l.product_id, l.brand:id ) }" />
		</MvIF>
	</MvIF>
	<MvIF EXPR = "{ l.brand_or_type EQ 'type' }">
		<MvASSIGN NAME = "l.type" VALUE = "{ Load_Type_Code( l.value ) }">
		<MvIF EXPR = "{ l.type:id }">
			<MvFUNCTIONRETURN VALUE = "{ UpdateProductType( l.product_id, l.type:id ) }" />
		</MvIF>
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Return_Product_Brand_Or_Type" PARAMETERS = "product_id, brand_or_type" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.product_id" VALUE = "{ trim( l.product_id ) }">
	<MvASSIGN NAME = "l.brand_or_type" VALUE = "{ trim( l.brand_or_type ) }">
	<MvIF EXPR = "{ ISNULL l.product_id OR ISNULL l.brand_or_type }">
		<MvFUNCTIONRETURN VALUE = "">
	</MvIF>
	<MvIF EXPR = "{ l.brand_or_type EQ 'brand' }">
		<MvEVAL EXPR = "{ Get_Product_Brand( l.product_id, l.brand ) }">
		<MvIF EXPR = "{ l.brand:code }">
			<MvFUNCTIONRETURN VALUE = "{ l.brand:code }" />
		</MvIF>
	</MvIF>
	<MvIF EXPR = "{ l.brand_or_type EQ 'type' }">
		<MvASSIGN NAME = "l.type" VALUE = "{ Get_Product_Type( l.product_id ) }">
		<MvIF EXPR = "{ l.type:code }">
			<MvFUNCTIONRETURN VALUE = "{ l.type:code }" />
		</MvIF>
	</MvIF>
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>